# Maintainer: Anatol Pomozov
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=glibc
pkgver=2.35
pkgrel=1
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc="GNU C Library"
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
depends=(linux-api-headers)
makedepends=(git gcc-glibc python)
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig}
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun')
sha256sums=('5123732f6b67ccd319305efd399971d58592122bcc2a6518a1bd2510dd0cf52e'
            'SKIP'
            '0a25d0358ba67c9cad1d94707615d9a6249b5234a9de601a10f281a5c604cc83'
            '0cc8d30b73368bfb92f7ea8298e4a791bb8d641c97ec86ef1c1be6052920fe32'
            '69a74af5419df4b914190b29cedee762413716fdc397eee22d7dcb5a5efdb092'
            '55871505df12af8eea2e6d9d50d1337437d12f082f6fd2c673f9911cb9747938'
            '55365e5bf9d758ded46e82fd6165b6a1c7828ab96d9de18346653553fce3018d'
            '47a5eed52a0c91e86b7f26e5e716ce24b8eb545eee21dc17f98cafc1f4a2dbb4'
            'e4eb6b3a3b42b28e15010c7a062141f8fd262c85185a746639f4432594fe9a1d'
            'f35852b69cd3fc78f7e77b566075b24e7a641dd60484e05abfcb838294225d1a'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            'ecb37a575c55dadcf70e9ca6fc7bc6e8694b6693f65a59a5264f264422f61bc0')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  mkdir -p glibc-build

  [[ ! -d glibc ]] && ln -s glibc-$pkgver glibc

  sed -i 's|/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /tmp/ glibc-$pkgver | grep -v /var/ | awk -F'[:=]' '{printf $1 "\n"}')
  sed -i 's|/var/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /var/tmp glibc-$pkgver | awk -F'[:=]' '{printf $1 "\n"}')
}

build() {
  cd glibc-build
  echo "slibdir=${prefix}/glibc/lib" > configparms
  echo "rtlddir=${prefix}/glibc/lib" >> configparms
  echo "sbindir=${prefix}/glibc/bin" >> configparms
  echo "rootsbindir=${prefix}/glibc/bin" >> configparms

  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=2 -O2 -pipe/}

  ../glibc-$pkgver/configure \
      --prefix=${prefix}/glibc \
      --libdir=${prefix}/glibc/lib \
      --libexecdir=${prefix}/glibc/lib \
      --with-headers=${prefix}/glibc/include \
      --with-bugurl=https://github.com/Maxython/glibc-for-termux/issues \
      --enable-bind-now \
      --enable-cet \
      --enable-kernel=4.4 \
      --enable-multi-arch \
      --enable-kernel=3.18.139 \
      --enable-stack-protector=strong \
      --enable-systemtap \
      --disable-profile \
      --disable-crypt \
      --disable-werror

  echo "build-programs=no" >> configparms
  make -O

  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS=-Wp,-D_FORTIFY_SOURCE=2 -O2 -pipe" >> configparms
  make -O
  make info

  locale/localedef -c -f ../glibc/localedata/charmaps/UTF-8 -i ../glibc/localedata/locales/C ../C.UTF-8/
}

package() {
  backup=(data/data/com.termux/files/usr/glibc/etc/gai.conf
          data/data/com.termux/files/usr/glibc/etc/locale.gen
          data/data/com.termux/files/usr/glibc/etc/nscd.conf)

  make -C glibc-build install_root="$pkgdir" install
  rm -f "$terdir"/usr/glibc/etc/ld.so.cache

  # Shipped in tzdata
  rm -f "$pkgdir"/usr/bin/{tzselect,zdump,zic}

  cd glibc

  install -dm755 "$terdir"/usr/glibc/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$terdir/usr/glibc/etc/nscd.conf"
  install -m644 nscd/nscd.service "$terdir/usr/glibc/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$terdir/usr/glibc/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$terdir/usr/glibc/var/db/nscd"

  install -m644 posix/gai.conf "$terdir"/usr/glibc/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$terdir/usr/glibc/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$terdir/usr/glibc/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc/localedata/SUPPORTED" >> "$terdir/usr/glibc/etc/locale.gen"

  # install C.UTF-8 so that it is always available
  install -dm755 "$terdir/usr/glibc/lib/locale"
  cp -r "$srcdir/C.UTF-8" -t "$terdir/usr/glibc/lib/locale"
  sed -i '/#C\.UTF-8 /d' "$terdir/usr/glibc/etc/locale.gen"

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$terdir/usr/glibc/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$terdir/usr/glibc/include/sys/sdt-config.h"

  sed -i 's|!/bin/bash|!/data/data/com.termux/files/usr/bin/bash|' "$terdir"/usr/glibc/bin/*
  mkdir -p "$terdir"/usr/bin
  install -m755 "$srcdir/grun" "$terdir/usr/bin"
}
